/*
 * SPDX-FileCopyrightText: Copyright (c) 2026, NVIDIA CORPORATION & AFFILIATES. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

#include "../test_utils.cuh"

#include <raft/core/detail/macros.hpp>
#include <raft/core/dry_run_resources.hpp>
#include <raft/core/resource/cuda_stream.hpp>
#include <raft/core/resources.hpp>
#include <raft/util/cuda_rt_essentials.hpp>
#include <raft/util/kernel_launch.hpp>

#include <rmm/device_uvector.hpp>

#include <gtest/gtest.h>

#include <cstdint>
#include <regex>
#include <string>
#include <utility>

namespace raft {

namespace {

RAFT_KERNEL noop_kernel() {}

RAFT_KERNEL write_one_kernel(int* out)
{
  if (threadIdx.x == 0 && blockIdx.x == 0) { *out = 1; }
}

RAFT_KERNEL write_count_kernel(int* out, std::uint32_t n)
{
  if (threadIdx.x == 0 && blockIdx.x == 0) { *out = static_cast<int>(n); }
}

RAFT_KERNEL copy_one_kernel(int const* in, int* out)
{
  if (threadIdx.x == 0 && blockIdx.x == 0) { *out = *in; }
}

/** The handle a runtime-linked library would hand out, for a kernel this test compiled itself. */
template <typename Kernel>
auto handle_of(Kernel* kernel) -> cudaKernel_t
{
  cudaKernel_t handle{};
  RAFT_CUDA_TRY(cudaGetKernel(&handle, reinterpret_cast<void const*>(kernel)));
  return handle;
}

RAFT_KERNEL copy_restricted_kernel(int const* __restrict__ in, int* out)
{
  if (threadIdx.x == 0 && blockIdx.x == 0) { *out = *in; }
}

void launch_write_one_with_restricted_pointer(raft::resources const& res, int* __restrict__ out)
{
  raft::launch_kernel(res, 1, 32, write_one_kernel, out);
}

RAFT_KERNEL smem_kernel(int* out)
{
  extern __shared__ int shared[];  // NOLINT(modernize-avoid-c-arrays)
  if (threadIdx.x == 0 && blockIdx.x == 0) {
    shared[0] = 1;
    __threadfence_block();
    *out = shared[0];
  }
}

/** Whether `w` can be launched as it is named, i.e. as an lvalue. */
template <typename W>
concept launchable_as_named = requires(W w)
{
  raft::launch_kernel(w, dim3{}, dim3{}, noop_kernel);
};

/** Whether `w` can be launched after being moved from. */
template <typename W>
concept launchable_when_moved = requires(W w)
{
  raft::launch_kernel(std::move(w), dim3{}, dim3{}, noop_kernel);
};

// Only a prvalue built inside the launch expression may be launched, so that the reported location
// is always the one of the launch. Everything else must fail to compile.
static_assert(launchable_as_named<raft::resources&>,
              "resources must convert to a launch_on prvalue");
static_assert(launchable_as_named<rmm::cuda_stream_view>,
              "a stream view must convert to a launch_on prvalue");
static_assert(launchable_as_named<cudaStream_t>,
              "a raw stream handle must convert to a launch_on prvalue");
static_assert(!launchable_as_named<raft::launch_on>, "a stored launch_on must not be launchable");
static_assert(!launchable_as_named<raft::launch_on&>, "an lvalue launch_on must not be launchable");
static_assert(!launchable_when_moved<raft::launch_on>,
              "a moved-from launch_on must not be launchable");

/** Whether a kernel named at run time by `Signature` can be launched with `Args`. */
template <typename Signature, typename... Args>
concept launchable_at_runtime = requires(raft::resources & res, cudaKernel_t handle, Args... args)
{
  raft::launch_kernel(res, dim3{}, dim3{}, raft::kernel_ref<Signature>{handle}, args...);
};

// Unlike the function-pointer overloads, the runtime-kernel one has no exactly-matching sibling to
// defer to, so it must accept arguments that already have the parameter types.
static_assert(launchable_at_runtime<void(int*), int*>,
              "an argument that already has the parameter type must be accepted");
static_assert(launchable_at_runtime<void(int const*), int*>,
              "an argument that converts to its parameter must be accepted");
static_assert(!launchable_at_runtime<void(int*, int), int*>, "too few arguments must not compile");
static_assert(!launchable_at_runtime<void(int*), int*, int>, "too many arguments must not compile");

}  // namespace

TEST(KernelLaunch, SuccessfulLaunch)
{
  raft::resources res;
  rmm::device_uvector<int> out(1, resource::get_cuda_stream(res));
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), resource::get_cuda_stream(res)));

  raft::launch_kernel(res, 1, 32, write_one_kernel, out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, RestrictedPointerArgument)
{
  raft::resources res;
  rmm::device_uvector<int> out(1, resource::get_cuda_stream(res));
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), resource::get_cuda_stream(res)));

  launch_write_one_with_restricted_pointer(res, out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, ConvertedRestrictedPointerArgument)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  rmm::device_uvector<int> in(1, stream);
  rmm::device_uvector<int> out(1, stream);
  int host_in = 1;
  RAFT_CUDA_TRY(cudaMemcpyAsync(in.data(), &host_in, sizeof(int), cudaMemcpyHostToDevice, stream));

  raft::launch_kernel(res, 1, 32, copy_restricted_kernel, in.data(), out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, StreamOverload)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  EXPECT_NO_THROW(raft::launch_kernel(stream, 1, 1, noop_kernel));
  resource::sync_stream(res);
}

TEST(KernelLaunch, RawStreamHandleOverload)
{
  raft::resources res;
  cudaStream_t stream = resource::get_cuda_stream(res).value();
  EXPECT_NO_THROW(raft::launch_kernel(stream, 1, 1, noop_kernel));
  resource::sync_stream(res);
}

TEST(KernelLaunch, SharedMemory)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  rmm::device_uvector<int> out(1, stream);
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), stream));

  raft::launch_kernel({stream, sizeof(int)}, 1, 32, smem_kernel, out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, ErrorReportsCallSite)
{
  raft::resources res;

  // Intentionally invalid configuration: block size exceeds hardware limit.
  constexpr int k_bad_block = 2048;
  std::string caught;
  int launch_line = 0;
  try {
    launch_line = __LINE__ + 1;
    raft::launch_kernel(res, 1, k_bad_block, noop_kernel);
    FAIL() << "Expected cuda_error from invalid launch configuration";
  } catch (raft::cuda_error const& e) {
    caught = e.what();
  }

  // Must blame this test translation unit, not the launcher header.
  EXPECT_EQ(caught.find("kernel_launch.hpp"), std::string::npos) << caught;
  EXPECT_NE(caught.find("kernel_launch.cu"), std::string::npos) << caught;

  std::string re_exp{R"(CUDA error encountered at: file=.*kernel_launch\.cu line=)"};
  re_exp += std::to_string(launch_line);
  re_exp += R"( function=.*ErrorReportsCallSite.*: call='cudaLaunchKernelExC', Reason=.*)";
  EXPECT_TRUE(std::regex_search(caught, std::regex(re_exp)))
    << "message:'" << caught << "'\nexpected regex:'" << re_exp << "'";
}

TEST(KernelLaunch, DryRunSkipsLaunch)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  // Allocate and zero with the real resources: dry-run memory must never be written to.
  rmm::device_uvector<int> out(1, stream);
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), stream));
  resource::sync_stream(res);

  {
    raft::dry_run_resources dry_res(res);
    raft::launch_kernel(dry_res, 1, 32, write_one_kernel, out.data());
    resource::sync_stream(dry_res);
  }

  int host_out = -1;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 0) << "the kernel must not run in dry-run mode";
}

TEST(KernelLaunch, DryRunIgnoresBadConfig)
{
  raft::resources res;
  raft::dry_run_resources dry_res(res);

  // A skipped launch is not validated by CUDA, so even a bad configuration does not throw.
  constexpr int k_bad_block = 2048;
  EXPECT_NO_THROW(raft::launch_kernel(dry_res, 1, k_bad_block, noop_kernel));
}

TEST(KernelLaunch, SkipExecutionOnStream)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  rmm::device_uvector<int> out(1, stream);
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), stream));

  raft::launch_kernel({stream, 0, true}, 1, 32, write_one_kernel, out.data());
  resource::sync_stream(res);

  int host_out = -1;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 0) << "skip_execution must suppress the launch";
}

TEST(KernelLaunch, CooperativeLaunch)
{
  raft::resources res;
  rmm::device_uvector<int> out(1, resource::get_cuda_stream(res));
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), resource::get_cuda_stream(res)));

  raft::launch_kernel({res, 0, {raft::cooperative()}}, 1, 32, write_one_kernel, out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, CooperativeLaunchRejectsNonResidentGrid)
{
  raft::resources res;

  // A cooperative launch requires the whole grid to be resident at once, so a grid that launches
  // fine on its own must fail once the attribute reaches the driver. Without this asymmetry the
  // test could not tell a plumbed attribute from a dropped one.
  constexpr int k_huge_grid = 1 << 20;
  constexpr int k_block     = 1024;
  EXPECT_NO_THROW(raft::launch_kernel(res, k_huge_grid, k_block, noop_kernel));
  EXPECT_THROW(
    raft::launch_kernel({res, 0, {raft::cooperative()}}, k_huge_grid, k_block, noop_kernel),
    raft::cuda_error);
  resource::sync_stream(res);
}

TEST(KernelLaunch, SharedMemoryCarveout)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  rmm::device_uvector<int> out(1, stream);
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), stream));

  raft::launch_kernel(
    {res, sizeof(int), {raft::shmem_carveout(100)}}, 1, 32, smem_kernel, out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, MultipleAttributes)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  rmm::device_uvector<int> out(1, stream);
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), stream));

  raft::launch_kernel({res, sizeof(int), {raft::cooperative(), raft::shmem_carveout(50)}},
                      1,
                      32,
                      smem_kernel,
                      out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, AttributesInDryRunAreSkipped)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  rmm::device_uvector<int> out(1, stream);
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), stream));
  resource::sync_stream(res);

  auto launch = [&](raft::resources const& h) {
    raft::launch_kernel({h, 0, {raft::cooperative()}}, 1, 32, write_one_kernel, out.data());
  };

  {
    raft::dry_run_resources dry_res(res);
    launch(dry_res);
    resource::sync_stream(dry_res);
  }

  int host_out = -1;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 0) << "attributes must not make a dry-run launch execute";

  raft::execute_with_dry_run_check(res, launch, raft::alloc_behavior::NO_ALLOCATIONS);

  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1) << "the real pass must execute the kernel";
}

TEST(KernelLaunch, RuntimeKernelLaunch)
{
  raft::resources res;
  rmm::device_uvector<int> out(1, resource::get_cuda_stream(res));
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), resource::get_cuda_stream(res)));

  raft::launch_kernel(
    res, 1, 32, raft::kernel_ref<void(int*)>{handle_of(write_one_kernel)}, out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, RuntimeKernelConvertsArguments)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  rmm::device_uvector<int> out(1, stream);
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), stream));

  // A std::size_t into a std::uint32_t parameter: the conversion is what lets a call site drop the
  // casts that a launch taking the addresses of its arguments would need for the sizes to match.
  std::size_t const n = 7;
  raft::launch_kernel(res,
                      1,
                      32,
                      raft::kernel_ref<void(int*, std::uint32_t)>{handle_of(write_count_kernel)},
                      out.data(),
                      n);
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 7);
}

TEST(KernelLaunch, RuntimeKernelConvertsPointerArgument)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  rmm::device_uvector<int> in(1, stream);
  rmm::device_uvector<int> out(1, stream);
  int host_in = 1;
  RAFT_CUDA_TRY(cudaMemcpyAsync(in.data(), &host_in, sizeof(int), cudaMemcpyHostToDevice, stream));

  // `int*` into a `int const*` parameter.
  raft::launch_kernel(res,
                      1,
                      32,
                      raft::kernel_ref<void(int const*, int*)>{handle_of(copy_one_kernel)},
                      in.data(),
                      out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, CooperativeRuntimeKernel)
{
  raft::resources res;
  rmm::device_uvector<int> out(1, resource::get_cuda_stream(res));
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), resource::get_cuda_stream(res)));

  // The two features are independent: a runtime kernel takes its attributes from `launch_on` just
  // like a statically compiled one.
  raft::launch_kernel({res, 0, {raft::cooperative()}},
                      1,
                      32,
                      raft::kernel_ref<void(int*)>{handle_of(write_one_kernel)},
                      out.data());
  resource::sync_stream(res);

  int host_out = 0;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1);
}

TEST(KernelLaunch, RuntimeKernelDryRunSkipsLaunch)
{
  raft::resources res;
  auto stream = resource::get_cuda_stream(res);
  rmm::device_uvector<int> out(1, stream);
  RAFT_CUDA_TRY(cudaMemsetAsync(out.data(), 0, sizeof(int), stream));
  resource::sync_stream(res);

  auto handle = handle_of(write_one_kernel);
  auto launch = [&](raft::resources const& h) {
    raft::launch_kernel(h, 1, 32, raft::kernel_ref<void(int*)>{handle}, out.data());
  };

  {
    raft::dry_run_resources dry_res(res);
    launch(dry_res);
    resource::sync_stream(dry_res);
  }

  int host_out = -1;
  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 0) << "a runtime kernel must not run in dry-run mode";

  raft::execute_with_dry_run_check(res, launch, raft::alloc_behavior::NO_ALLOCATIONS);

  RAFT_CUDA_TRY(cudaMemcpy(&host_out, out.data(), sizeof(int), cudaMemcpyDeviceToHost));
  EXPECT_EQ(host_out, 1) << "the real pass must execute the kernel";
}

TEST(KernelLaunch, RuntimeKernelErrorReportsCallSite)
{
  raft::resources res;
  auto handle = handle_of(noop_kernel);

  constexpr int k_bad_block = 2048;
  std::string caught;
  int launch_line = 0;
  try {
    launch_line = __LINE__ + 1;
    raft::launch_kernel(res, 1, k_bad_block, raft::kernel_ref<void()>{handle});
    FAIL() << "Expected cuda_error from invalid launch configuration";
  } catch (raft::cuda_error const& e) {
    caught = e.what();
  }

  EXPECT_EQ(caught.find("kernel_launch.hpp"), std::string::npos) << caught;
  EXPECT_NE(caught.find("kernel_launch.cu"), std::string::npos) << caught;

  std::string re_exp{R"(CUDA error encountered at: file=.*kernel_launch\.cu line=)"};
  re_exp += std::to_string(launch_line);
  re_exp +=
    R"( function=.*RuntimeKernelErrorReportsCallSite.*: call='cudaLaunchKernelExC', Reason=.*)";
  EXPECT_TRUE(std::regex_search(caught, std::regex(re_exp)))
    << "message:'" << caught << "'\nexpected regex:'" << re_exp << "'";
}

}  // namespace raft
